comment - reduce program tr_lo;
comment - 1/12/15;
comment - lowest order contribution in the;
comment - three-photon-annihilation channel;
comment - at order m alpha^6;
comment;
comment - I am using k1 and k3 to stand for q and p k4 is r;
vector n,k1,k2,k3,k4;
index m1,m2,m3,m4,lam,kap,mu;
comment - m4 is my beta
comment *** define line operators;
operator ga, gb;
for all n let ga(n)=g(la,n)+1;
for all n let gb(n)=g(lb,n)+1;
comment;
let a=1-2*u;
let (n.n)=1;
let k2=2*n-k1-k3;
let ppp=(lam.kap)-(lam.n)*(n.kap);
let etta1 = -k4.k4 - 2*k3.k4 + 2*k4.n;
let etta2 = -k4.k4 + 2*k1.k4 - 2*k4.n;
let ffac = (k4.k4 + 2*k4.n)*g(la,k1)-(-2*k1.k4+k1.k1-2*k1.n)*ga(-n-k4);
let rfac=g(la,m1)*ga(-n+k1)*g(la,m4)*ffac*g(la,m2)*ga(n-k3-k4)*g(la,m4)*ga(n-k3)*g(la,m3)*ga(n)*g(la,lam);
comment;
on gc;
comment;
off output;

trra := rfac*ppp*g(lb,m3)*gb(n-k3)*g(lb,m2)*gb(-n+k1)*g(lb,m1)*gb(-n)*g(lb,kap);

trrb := rfac*ppp*g(lb,m1)*gb(n-k1)*g(lb,m3)*gb(-n+k2)*g(lb,m2)*gb(-n)*g(lb,kap);

trrc := rfac*ppp*g(lb,m2)*gb(n-k2)*g(lb,m1)*gb(-n+k3)*g(lb,m3)*gb(-n)*g(lb,kap);

trrd := rfac*ppp*g(lb,m1)*gb(n-k1)*g(lb,m2)*gb(-n+k3)*g(lb,m3)*gb(-n)*g(lb,kap);

trre := rfac*ppp*g(lb,m2)*gb(n-k2)*g(lb,m3)*gb(-n+k1)*g(lb,m1)*gb(-n)*g(lb,kap);

trrf := rfac*ppp*g(lb,m3)*gb(n-k3)*g(lb,m1)*gb(-n+k2)*g(lb,m2)*gb(-n)*g(lb,kap);


comment - k1->q, k2->s=2n-p-q, k3->p k4->r;

let k1.n=q0;
let k3.n=p0;
let k4.n=r0;
let k1.k1=q0*q0-qq;
let k3.k3=p0*p0-pp;
let k4.k4=r0*r0-rr;
let k1.k3=p0*q0-pq;
let k1.k4=q0*r0-qr;
let k3.k4=p0*r0-pr;

trra;
trrb;
trrc;
trrd;
trre;
trrf;

comment: output for calculating the imaginary part;

off fancy;
on fort;
off echo;
off period;
fort_width := 10000;

out trDvImOut;

tra := trra;
trb := trrb;
trc := trrc;
trd := trrd;
tre := trre;
trf := trrf;

shut trDvImOut;

comment: output for calculating the real part;

on period;
fort_width := 72;

on output;
out vcredint;

write "      double precision function tra(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";tra := trra;
write "      return";write "      end";write "c";
write "      double precision function tra100(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";tra100 := df(trra,p0);
write "      return";write "      end";write "c";
write "      double precision function tra010(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";tra010 := df(trra,q0);
write "      return";write "      end";write "c";
write "      double precision function tra110(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";tra110 := df(trra,p0,q0);
write "      return";write "      end";write "c";
write "      double precision function tra101(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";tra101 := df(trra,p0,r0);
write "      return";write "      end";write "c";
write "      double precision function tra001(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";tra001 := df(trra,r0);
write "      return";write "      end";write "c";
write "      double precision function tra011(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";tra011 := df(trra,q0,r0);
write "      return";write "      end";write "c";
write "      double precision function tra111(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";tra111 := df(trra,p0,q0,r0);
write "      return";write "      end";write "c";
write "c";

write "      double precision function trb(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trb := trrb;
write "      return";write "      end";write "c";
write "      double precision function trb100(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trb100 := df(trrb,p0);
write "      return";write "      end";write "c";
write "      double precision function trb010(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trb010 := df(trrb,q0);
write "      return";write "      end";write "c";
write "      double precision function trb110(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trb110 := df(trrb,p0,q0);
write "      return";write "      end";write "c";
write "      double precision function trb101(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trb101 := df(trrb,p0,r0);
write "      return";write "      end";write "c";
write "      double precision function trb001(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trb001 := df(trrb,r0);
write "      return";write "      end";write "c";
write "      double precision function trb011(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trb011 := df(trrb,q0,r0);
write "      return";write "      end";write "c";
write "      double precision function trb111(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trb111 := df(trrb,p0,q0,r0);
write "      return";write "      end";write "c";
write "c";

write "      double precision function trc(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trc := trrc;
write "      return";write "      end";write "c";
write "      double precision function trc100(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trc100 := df(trrc,p0);
write "      return";write "      end";write "c";
write "      double precision function trc010(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trc010 := df(trrc,q0);
write "      return";write "      end";write "c";
write "      double precision function trc110(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trc110 := df(trrc,p0,q0);
write "      return";write "      end";write "c";
write "      double precision function trc101(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trc101 := df(trrc,p0,r0);
write "      return";write "      end";write "c";
write "      double precision function trc001(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trc001 := df(trrc,r0);
write "      return";write "      end";write "c";
write "      double precision function trc011(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trc011 := df(trrc,q0,r0);
write "      return";write "      end";write "c";
write "      double precision function trc111(p0,q0,r0,pp,qq,rr,pq,pr,qr)";write "      implicit double precision (a-z)";trc111 := df(trrc,p0,q0,r0);
write "      return";write "      end";write "c";
write "c";
write "c";
write "c";


shut vcredint;
;end;